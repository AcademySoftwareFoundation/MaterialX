//
// Copyright Contributors to the MaterialX Project
// SPDX-License-Identifier: Apache-2.0
//

mdl 1.6;

import ::math::*;

// https://www.shadertoy.com/view/4djSRW
float2 mx_hextile_hash(float2 p)
{
    float3 p3 = math::frac(float3(p.x, p.y, p.x) * float3(0.1031, 0.1030, 0.0973));
    p3 += math::dot(p3, float3(p3.y, p3.z, p3.x) + 33.33);
    return math::frac((float2(p3.x, p3.x) + float2(p3.y, p3.z)) * float2(p3.z, p3.y));
}

// Christophe Schlick. “Fast Alternatives to Perlin’s Bias and Gain Functions”.
// In Graphics Gems IV, Morgan Kaufmann, 1994, pages 401–403.
// https://dept-info.labri.fr/~schlick/DOC/gem2.html
export float mx_schlick_gain(float x, float r)
{
    float rr = math::clamp(r, 0.001, 0.999);  // to avoid glitch
    float a = (1.0 / rr - 2.0) * (1.0 - 2.0 * x);
    return (x < 0.5) ? x / (a + 1.0) : (a - x) / (a - 1.0);
}

export struct mx_hextile_data
{
    float2 coord1;
    float2 coord2;
    float2 coord3;
    float3 weights;
    float rot_radian1;
    float rot_radian2;
    float rot_radian3;
};

// Morten S. Mikkelsen, Practical Real-Time Hex-Tiling, Journal of Computer Graphics
// Techniques (JCGT), vol. 11, no. 2, 77-94, 2022
// http://jcgt.org/published/0011/03/05/
export mx_hextile_data mx_hextile_coord(
    float2 coord,
    float rotation,
    float2 rotation_range,
    float scale,
    float2 scale_range,
    float offset,
    float2 offset_range
)
{
    float sqrt3_2 = math::sqrt(3.0) * 2.0;

    // scale coord to maintain the original fit
    float2 st = coord * sqrt3_2;

    // skew input space into simplex triangle grid
    // (1, 0, -tan(30), 2*tan(30))
    float2x2 to_skewed = float2x2(1.0, 0.0, -0.57735027, 1.15470054);
    float2 st_skewed = to_skewed * st;

    // barycentric weights
    float2 st_frac = math::frac(st_skewed);
    float3 temp = float3(st_frac.x, st_frac.y, 0.0);
    temp.z = 1.0 - temp.x - temp.y;

    float s = math::step(0.0, -temp.z);
    float s2 = 2.0 * s - 1.0;

    float w1 = -temp.z * s2;
    float w2 = s - temp.y * s2;
    float w3 = s - temp.x * s2;

    // vertex IDs
    int2 base_id = int2(math::floor(st_skewed));
    int si = int(s);
    int2 id1 = base_id + int2(si, si);
    int2 id2 = base_id + int2(si, 1 - si);
    int2 id3 = base_id + int2(1 - si, si);

    // tile center
    float2x2 inv_skewed = float2x2(1.0, 0.0, 0.5, 1.0 / 1.15470054);
    float2 ctr1 = inv_skewed * float2(id1) / float2(sqrt3_2);
    float2 ctr2 = inv_skewed * float2(id2) / float2(sqrt3_2);
    float2 ctr3 = inv_skewed * float2(id3) / float2(sqrt3_2);

    // reuse hash for performance
    float2 seed_offset = float2(0.12345);  // to avoid some zeros
    float2 rand1 = mx_hextile_hash(float2(id1) + seed_offset);
    float2 rand2 = mx_hextile_hash(float2(id2) + seed_offset);
    float2 rand3 = mx_hextile_hash(float2(id3) + seed_offset);

    // randomized rotation matrix
    float2 rr = math::radians(rotation_range);
    float rv1 = math::lerp(rr.x, rr.y, rand1.x * rotation);
    float rv2 = math::lerp(rr.x, rr.y, rand2.x * rotation);
    float rv3 = math::lerp(rr.x, rr.y, rand3.x * rotation);
    float sin_r1 = math::sin(rv1);
    float sin_r2 = math::sin(rv2);
    float sin_r3 = math::sin(rv3);
    float cos_r1 = math::cos(rv1);
    float cos_r2 = math::cos(rv2);
    float cos_r3 = math::cos(rv3);
    float2x2 rm1 = float2x2(cos_r1, -sin_r1, sin_r1, cos_r1);
    float2x2 rm2 = float2x2(cos_r2, -sin_r2, sin_r2, cos_r2);
    float2x2 rm3 = float2x2(cos_r3, -sin_r3, sin_r3, cos_r3);

    // randomized scale
    float2 sr = scale_range;
    float2 scale1 = math::lerp(1.0, math::lerp(sr.x, sr.y, rand1.y), scale);
    float2 scale2 = math::lerp(1.0, math::lerp(sr.x, sr.y, rand2.y), scale);
    float2 scale3 = math::lerp(1.0, math::lerp(sr.x, sr.y, rand3.y), scale);

    // randomized offset
    float2 offset1 = math::lerp(float2(offset_range.x), float2(offset_range.y), rand1 * offset);
    float2 offset2 = math::lerp(float2(offset_range.x), float2(offset_range.y), rand2 * offset);
    float2 offset3 = math::lerp(float2(offset_range.x), float2(offset_range.y), rand3 * offset);    

    mx_hextile_data tile_data;
    tile_data.weights = float3(w1, w2, w3);
    tile_data.rot_radian1 = rv1;
    tile_data.rot_radian2 = rv2;
    tile_data.rot_radian3 = rv3;    

    // get coord
    tile_data.coord1 = ((coord - ctr1) * rm1 / scale1) + ctr1 + offset1;
    tile_data.coord2 = ((coord - ctr2) * rm2 / scale2) + ctr2 + offset2;
    tile_data.coord3 = ((coord - ctr3) * rm3 / scale3) + ctr3 + offset3;
    
    return tile_data;
}
