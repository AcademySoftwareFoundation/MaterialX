set(MATERIALX_DATA_LIBRARY_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
set(MATERIALX_DATA_LIBRARY_BUILD_DIR ${CMAKE_CURRENT_BINARY_DIR}/mxDataLibrary)

file(GLOB_RECURSE MATERIALX_DATA_LIBRARY_SOURCE_FILES
        RELATIVE ${MATERIALX_DATA_LIBRARY_SOURCE_DIR}
        CONFIGURE_DEPENDS
        LIST_DIRECTORIES false
        *.mtlx
        *.md
        *.glsl
        *.osl
        *.h
        *.metal)

function(mx_build_data_library TARGET_NAME SOURCE_DIR DEST_DIR RELATIVE_SOURCE_FILES)
    foreach(RELATIVE_SOURCE_FILE IN LISTS RELATIVE_SOURCE_FILES)
        set(SOURCE_FILEPATH ${SOURCE_DIR}/${RELATIVE_SOURCE_FILE})
        set(DEST_FILEPATH ${DEST_DIR}/${RELATIVE_SOURCE_FILE})
        add_custom_command(
                OUTPUT ${DEST_FILEPATH}
                COMMAND ${CMAKE_COMMAND} -E copy_if_different ${SOURCE_FILEPATH} ${DEST_FILEPATH}
                DEPENDS ${SOURCE_FILEPATH})
        list(APPEND MATERIALX_DATA_LIBRARY_BUILD_FILES ${DEST_FILEPATH})
    endforeach()

    add_custom_target(${TARGET_NAME} ALL
            DEPENDS ${MATERIALX_DATA_LIBRARY_BUILD_FILES})
endfunction()

mx_build_data_library(buildMaterialXDataLibrary ${MATERIALX_DATA_LIBRARY_SOURCE_DIR} ${MATERIALX_DATA_LIBRARY_BUILD_DIR} "${MATERIALX_DATA_LIBRARY_SOURCE_FILES}")

# export the variables for use to install the data library for MaterialXTest
set(MATERIALX_DATA_LIBRARY_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR} PARENT_SCOPE)
set(MATERIALX_DATA_LIBRARY_SOURCE_FILES ${MATERIALX_DATA_LIBRARY_SOURCE_FILES} PARENT_SCOPE)

if(NOT SKBUILD)
    install(DIRECTORY ${MATERIALX_DATA_LIBRARY_BUILD_DIR}/
            DESTINATION "${MATERIALX_INSTALL_STDLIB_PATH}")
endif()

if(MATERIALX_BUILD_PYTHON)
    set(MATERIALX_PYTHON_LIBRARIES_PATH "${MATERIALX_PYTHON_FOLDER_NAME}/${MATERIALX_INSTALL_STDLIB_PATH}")
    if(SKBUILD)
        set(MATERIALX_PYTHON_LIBRARIES_PATH "${SKBUILD_PLATLIB_DIR}/MaterialX/libraries")
    endif()

    install(DIRECTORY ${MATERIALX_DATA_LIBRARY_BUILD_DIR}/
            DESTINATION "${MATERIALX_PYTHON_LIBRARIES_PATH}")
endif()
