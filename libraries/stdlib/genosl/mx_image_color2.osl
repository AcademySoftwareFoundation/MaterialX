#include "$fileTransformUv"

void mx_image_color2(string file, string layer, color2 default_value, vector2 texcoord, string uaddressmode, string vaddressmode, string  filtertype, string framerange, int frameoffset, string frameendaction, output color2 out)
{
    if (file == "" ||
        (uaddressmode == "constant" && (texcoord.x<0.0 || texcoord.x>1.0)) ||
        (vaddressmode == "constant" && (texcoord.y<0.0 || texcoord.y>1.0)))
    {
        out = default_value;
        return;
    }

    color missingColor = color(default_value.r, default_value.a, 0.0);
    vector2 st = mx_transform_uv(texcoord);
    color rgb = texture(file, st.x, st.y, "subimage", layer, "missingcolor", missingColor, "swrap", uaddressmode, "twrap", vaddressmode);

    out.r = rgb[0];
    out.a = rgb[1];
}
