void mx_set_ci (
    int output_type,

    // the per-type inputs must be named with "input_<typename>"
    // the of these inputs defines their corresponding output_type value.
    // this is dynamically derived in the shader generation code by
    // inspecting the order of the inputs in the node definition
    surfaceshader input_surfaceshader,
    MATERIAL input_material,
    BSDF input_BSDF,
    EDF input_EDF,
    float input_float,
    color input_color3,
    color4 input_color4,
    vector2 input_vector2,
    vector input_vector3,
    vector4 input_vector4,
    int input_integer,
    int input_boolean,
    matrix input_matrix33,
    matrix input_matrix44,
    displacementshader input_displacementshader,

    output closure color out_ci
)
{
    color c = 0;
    float a = 1;

    if (output_type == 0) {
        float opacity_weight = clamp(input_surfaceshader.opacity, 0.0, 1.0);
        out_ci =  (input_surfaceshader.bsdf + input_surfaceshader.edf) * opacity_weight + transparent() * (1.0 - opacity_weight);
    } else if (output_type == 1) {
        out_ci = input_material;
    } else if (output_type == 2) {
        out_ci = input_BSDF;
    } else if (output_type == 3) {
        out_ci = input_EDF;
    } else {
        if (output_type == 4) {
            c = input_float;
        } else if (output_type == 5) {
            c = input_color3;
        } else if (output_type == 6) {
            c = input_color4.rgb;
            a = input_color4.a;
        } else if (output_type == 7) {
            c = color(input_vector2.x, input_vector2.y, 0);
        } else if (output_type == 8) {
            c = color(input_vector3);
        } else if (output_type == 9) {
            c = color(input_vector4.x, input_vector4.y, input_vector4.z);
            a = input_vector4.w;
        } else if (output_type == 10) {
            c = color(input_integer);
        } else if (output_type == 11) {
            c = color(input_boolean);
        } else if (output_type == 12) {
            c = color(input_matrix33[0][0], input_matrix33[1][1], input_matrix33[2][2]);
        } else if (output_type == 13) {
            c = color(input_matrix44[0][0], input_matrix44[1][1], input_matrix44[2][2]);
            a = input_matrix44[4][4];
        } else if (output_type == 14) {
            c = color(input_displacementshader);
        }
        out_ci = c * a * emission() + (1-a) * transparent();
    }

    Ci = out_ci;
}
