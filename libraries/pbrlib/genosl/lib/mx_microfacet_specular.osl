#include "pbrlib/genosl/lib/mx_microfacet.osl"

// https://media.disneyanimation.com/uploads/production/publication_asset/48/asset/s2012_pbs_disney_brdf_notes_v3.pdf
// Appendix B.2 Equation 15
vector mx_ggx_importance_sample_NDF(vector2 Xi, vector X, vector Y, vector N, float alphaX, float alphaY)
{
    float phi = 2.0 * M_PI * Xi.x;
    float tanTheta = sqrt(Xi.y / (1.0 - Xi.y));
    vector H = X * (tanTheta * alphaX * cos(phi)) +
               Y * (tanTheta * alphaY * sin(phi)) +
               N;
    return normalize(H);
}

// Height-correlated Smith masking-shadowing
// http://jcgt.org/published/0003/02/03/paper.pdf
// Equations 72 and 99
float mx_ggx_smith_G2(float NdotL, float NdotV, float alpha)
{
    float alpha2 = mx_square(alpha);
    float lambdaL = sqrt(alpha2 + (1.0 - alpha2) * mx_square(NdotL));
    float lambdaV = sqrt(alpha2 + (1.0 - alpha2) * mx_square(NdotV));
    return 2.0 / (lambdaL / NdotL + lambdaV / NdotV);
}

// Rational quadratic fit to Monte Carlo data for GGX directional albedo.
color mx_ggx_dir_albedo(float NdotV, float roughness, color F0, color F90)
{
    float x = NdotV;
    float y = roughness;
    float x2 = mx_square(NdotV);
    float y2 = mx_square(roughness);
    vector4 r = vector4(0.10901, 0.92163, 1.0, 1.0) +
                vector4(-0.72019, -2.29412, -1.81963, -0.02130) * x +
                vector4(9.54750, 1.78914, 8.10489, 15.21516) * y +
                vector4(-0.93177, -2.62475, 12.89250, -45.75644) * x * y +
                vector4(29.60456, 1.40796, 29.11995, 13.53548) * x2 +
                vector4(-8.10263, -0.48479, -7.40042, 34.87345) * y2 +
                vector4(-27.96958, 0.71494, -36.69717, 26.50811) * x2 * y +
                vector4(18.28144, -0.50156, 13.79722, 253.01230) * x * y2 +
                vector4(-4.85579, 1.22080, 32.57057, -162.71012) * x2 * y2;
    vector2 AB = vector2(r.x, r.y) / vector2(r.z, r.w);
    return F0 * AB.x + F90 * AB.y;
}

float mx_ggx_dir_albedo(float NdotV, float roughness, float F0, float F90)
{
    color result = mx_ggx_dir_albedo(NdotV, roughness, color(F0), color(F90));
    return result[0];
}

float mx_ggx_dir_albedo(float NdotV, float roughness, float ior)
{
    color result = mx_ggx_dir_albedo(NdotV, roughness, color(mx_ior_to_f0(ior)), color(1.0));
    return result[0];
}

// https://blog.selfshadow.com/publications/turquin/ms_comp_final.pdf
// Equations 14 and 16
color mx_ggx_energy_compensation(float NdotV, float roughness, color Fss)
{
    float Ess = mx_ggx_dir_albedo(NdotV, roughness, 1.0, 1.0);
    return 1.0 + Fss * (1.0 - Ess) / Ess;
}

float mx_ggx_energy_compensation(float NdotV, float roughness, float Fss)
{
    color result = mx_ggx_energy_compensation(NdotV, roughness, color(Fss));
    return result[0];
}
