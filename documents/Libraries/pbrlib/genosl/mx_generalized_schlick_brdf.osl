#include "pbrlib/genosl/lib/mx_fresnel.osl"

void mx_generalized_schlick_brdf(float weight, color color0, color color90, float exponent, roughnessinfo roughness, normal N, vector U, string distribution, BSDF base, output BSDF result)
{
    if (weight < M_FLOAT_EPS)
    {
        result = base;
        return;
    }

    // Calculate fresnel since we need 
    // to attenuate the base layer according to fresnel
    //
    // Fresnel should be based on microfacet normal
    // but we have no access to that from here, so just use
    // view direction and surface normal instead
    //
    float NdotV = fabs(dot(N,-I));
    color F = mx_fresnel_schlick(NdotV, color0, color90, exponent);
    F *= weight;
    float avgF = dot(F, color(1.0 / 3.0));

    result = F * microfacet(distribution, N, U, roughness.alphaX, roughness.alphaY, 0.0, false)
             + base * (1.0 - avgF);
}
