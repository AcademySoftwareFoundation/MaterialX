#include "sxpbrlib/sx-osl/lib/sx_complexior.osl"
#include "sxpbrlib/sx-osl/lib/sx_fresnel.osl"

void sx_metalbsdf(color reflectivity, color edgetint, float roughness, float anisotropy, normal N, vector U, string distribution, output BSDF result)
{
    float alpha = clamp(roughness*roughness, M_FLOAT_EPS, 1.0);
    float alphaX = alpha;
    float alphaY = alpha;
    if (anisotropy > 0.0)
    {
        float aspect = clamp(anisotropy, 0.0, 0.98);
        aspect = sqrt(1.0 - aspect);
        alphaX = min(alpha / aspect, 1.0);
        alphaY = alpha * aspect;
    }

    // Calculate conductor fresnel
    //
    // Fresnel should be based on microfacet normal
    // but we have no access to that from here, so just use
    // view direction and surface normal instead
    //
    float NdotV = fabs(dot(N,-I));
    vector ior_n, ior_k;
    sx_complexior(reflectivity, edgetint, ior_n, ior_k);
    color F = sx_fresnel_conductor(NdotV, ior_n, ior_k);

    // Set ior to 0.0 to disable the internal dielectric fresnel
    result = F * microfacet(distribution, N, U, alphaX, alphaY, 0.0, false);
}
