#include "sxpbrlib/sx-osl/lib/sx_complexior.osl"
#include "sxpbrlib/sx-osl/lib/sx_fresnel.osl"

void sx_conductorbrdf(float weight, color reflectivity, color edgecolor, vector2 roughness, normal N, vector U, string distribution, output BSDF result)
{
    if (weight < M_FLOAT_EPS)
    {
        result = 0;
        return;
    }

    // Calculate conductor fresnel
    //
    // Fresnel should be based on microfacet normal
    // but we have no access to that from here, so just use
    // view direction and surface normal instead
    //
    float NdotV = fabs(dot(N,-I));
    vector ior_n, ior_k;
    sx_complexior(reflectivity, edgecolor, ior_n, ior_k);
    color F = sx_fresnel_conductor(NdotV, ior_n, ior_k);
    F *= weight;

    // Set ior to 0.0 to disable the internal dielectric fresnel
    result = F * microfacet(distribution, N, U, roughness.x, roughness.y, 0.0, false);
}
